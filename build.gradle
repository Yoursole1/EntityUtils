plugins {
    id 'java-library'
    id 'checkstyle'
    id 'maven-publish'
    id 'io.papermc.paperweight.userdev' version libs.versions.paperweight
    id 'com.github.johnrengelman.shadow' version libs.versions.shadow
    id 'xyz.jpenilla.run-paper' version '1.0.6'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.2'
}

group 'org.entityutils'
version '0.1.2-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    paperweightDevelopmentBundle libs.paper.devbundle

    compileOnly "org.projectlombok:lombok:1.18.24"
    annotationProcessor "org.projectlombok:lombok:1.18.24"
}


java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks {
    assemble {
        dependsOn(reobfJar)
    }
    build {
        dependsOn(shadowJar)
        dependsOn(reobfJar)
    }
    compileJava {
        options.release.set(17)
        options.encoding = "UTF-8"
    }

    checkstyleMain {
        source = 'src/main/java'
    }

    checkstyleTest {
        source = 'src/test/java'
    }
}

checkstyle {
    configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Yoursole1/EntityUtils")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

bukkit {
    load = 'STARTUP'
    main = 'org.entityutils.EntityUtilsPlugin'
    prefix = 'EntityUtils'
    apiVersion = '1.18'
    defaultPermission = 'OP'
    authors = ['MarvelousAnything', 'Yoursole']
}

